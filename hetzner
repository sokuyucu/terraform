
provider "hcloud" {
	token = var.kae_hcloud_token
}

resource "hcloud_ssh_key" "kubeone" {
  name       = "kubeone-${var.kae_cluster_name}"
  public_key = file(var.kae_ssh_public_key_file)
}

resource "hcloud_network" "net" {
  name     = var.kae_cluster_name
  ip_range = var.kae_ip_range
}

resource "hcloud_network_subnet" "kubeone" {
  network_id   = hcloud_network.net.id
  type         = "server"
  network_zone = var.kae_network_zone
  ip_range     = var.kae_ip_range
}

resource "hcloud_server_network" "control_plane" {
  count     = 3
  server_id = element(hcloud_server.control_plane.*.id, count.index)
  subnet_id = hcloud_network_subnet.kubeone.id
}

resource "hcloud_server" "control_plane" {
  count       = var.kae_control_plane_replicas
  name        = "${var.kae_cluster_name}-kaemaster-${count.index + 1}"
  server_type = var.kae_control_plane_type
  image       = var.kae_image
  location    = var.kae_datacenter



  ssh_keys = [
    hcloud_ssh_key.kubeone.id,
  ]

  labels = {
    "kubeone_cluster_name" = var.kae_cluster_name
    "role"                 = "api"
  }
}




resource "hcloud_load_balancer_network" "load_balancer" {
  load_balancer_id = hcloud_load_balancer.load_balancer.id
  subnet_id        = hcloud_network_subnet.kubeone.id
}

resource "hcloud_load_balancer" "load_balancer" {
  name               = "${var.kae_cluster_name}-kaelb"
  load_balancer_type = var.kae_lb_type
  location           = var.kae_datacenter

  labels = {
    "kubeone_cluster_name" = var.kae_cluster_name
    "role"                 = "lb"
  }
}

resource "hcloud_load_balancer_target" "load_balancer_target" {
  type             = "server"
  load_balancer_id = hcloud_load_balancer.load_balancer.id
  count            = 3
  server_id        = element(hcloud_server.control_plane.*.id, count.index)
  use_private_ip   = true
  depends_on = [
    hcloud_server_network.control_plane,
    hcloud_load_balancer_network.load_balancer
  ]
}

resource "hcloud_load_balancer_service" "load_balancer_service" {
  load_balancer_id = hcloud_load_balancer.load_balancer.id
  protocol         = "tcp"
  listen_port      = 6443
  destination_port = 6443
}
